buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7")
        classpath("com.netflix.nebula:nebula-publishing-plugin:9.5.0")
        classpath("net.researchgate:gradle-release:2.8.0")
        classpath("com.bmuschko:gradle-docker-plugin:4.6.2")
    }
}


apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

apply plugin: 'jacoco'
apply plugin: "org.sonarqube"

apply plugin: 'nebula.javadoc-jar'
apply plugin: 'nebula.source-jar'
apply plugin: 'nebula.maven-publish'

apply plugin: "com.bmuschko.docker-remote-api"

apply plugin: "net.researchgate.release"
apply plugin: "maven"

group 'io.maslick'
version '1.0' + (Boolean.valueOf(System.getProperty("snapshot")) ? "-SNAPSHOT" : "")

sourceCompatibility = 11

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-starter-web")
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("junit:junit:4.12")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

publishing.repositories {
    maven {
        url project.version.endsWith('-SNAPSHOT') ? System.getProperty("nexusSnapshot") : System.getProperty("nexusRepo")
        credentials {
            username System.getProperty("nexusUser", "admin")
            password System.getProperty("nexusPassword", "admin123")
        }
    }
}

import com.bmuschko.gradle.docker.tasks.image.Dockerfile
task createDockerfile(type: Dockerfile) {
    destFile = project.file('build/libs/Dockerfile')
    from 'amazoncorretto:11'
    instruction 'LABEL maintainer="pavel.masloff@gmail.com"'
    exposePort 7777
    copyFile jar.archiveName, '/app/app.jar'
    workingDir '/app'
    instruction 'CMD java -jar app.jar'
}